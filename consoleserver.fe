#! /usr/bin/env ferite

uses 'console', 'eventloop';

class ConsoleServer {
	array commands;
	string prompt;
	
	function handleRequest () {
		string cmd_string = Console.readln();
		string cmd;
		if (cmd_string != '') {
			array args = Regexp.split('\s+',cmd_string);
			cmd = args.shift();
			
			if (cmd && .commands[cmd]) {
				.commands[cmd].invoke(args);
			}
			else if (.commands['unknown']) {
				args.unshift(cmd);
				.commands.unknown.invoke(args);
			}
			else {
				Console.println("Unknown command: $cmd");
			}
		}
		Console.print(.prompt);
		Console.stdout.flush();
	}
	
	function prompt (string text) {
		.prompt = text;
		Console.print(.prompt);
		Console.stdout.flush();
	}
	
	function command (...) {
		object handler = recipient();
		if (handler) {
			array names = arguments();
			names.each() using (n) {
				.commands[n] = handler;
			};
		}
	}
	
	function constructor () {
		Console.stdin.nonblock();
		EventLoop.add(Console.stdin,self);
	}
}
